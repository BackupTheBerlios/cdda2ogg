#!/usr/bin/perl -w
# 
#  CDDA2OGG
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
#
#  Authors: Waldemar Brodkorb <waldemar@thinknow.de>
#           Richard Höchenberger <GeekuX@web.de>
#
#  Program to encode Compact Disc Digital Audio to Ogg Vorbis
#

use strict;
use FreeDB::FreeDB;

#
# global vars
#

my $author = "Waldemar Brodkorb <waldemar\@thinknow.de>\nRichard Höchenberger <GeekuX\@web.de>";
my $cdda2ogg = $0; $cdda2ogg =~ s/.*\/([^\/]+)$/$1/;
my $version = "%%VERSION%%";
my $HOME = $ENV{HOME};

#
# programs & default flags
#

my $cdrom = "/dev/cdrom";

my $cdparanoia = `which cdparanoia`; $cdparanoia =~ s/\n//;
my $cdpflags_default = "";
my $cdpflags;

my $bitrate = "160";

my $oggenc = `which oggenc`; $oggenc =~ s/\n//;
my $oggencflags_default = "";
my $oggencflags;

my $trackname_default = "track";
my $trackname = "$trackname_default";

my $holdwav = 0;

my $cddbserver = "http://freedb.freedb.org/cgi-bin/cddb.cgi";
my $nocddb = 0;

my $artist;
my $album;
my @titles;
my $total_tracks;

my $quite = 0;
my $null = "2>&1 > /dev/null";
my $verbose = 0;
my $debug = 0;
my $success = 0;
my $answer = "";
my $all = 0;
my @riptracks;
my $output;

#
# the main program starts here
#

# when no arguments given, print usage and version

if ($#ARGV == -1) {

    &version; 
    print usage();

# else begin with the fun
    
} else {
    # parse configfiles and arguments
    parse_config_file("/etc/cdda2oggrc");
    parse_config_file("$HOME/.cdda2oggrc");
    parse_command_line(@ARGV);
    
    if ($all == 0 && ! @riptracks) {
        &version; 
        print usage();
        print "\n --------------------------------------------- \n";
        print "\n ------  What do you want to rip, geek? ------ \n";
        print "\n --------------------------------------------- \n";
        exit 1;
    }
    
    # check if encoder and ripper exists
    &check_apps;

    if ( ! @riptracks ) {
        $total_tracks = &get_disc_tracks; # get total sum of tracks with function
    } else {
        $total_tracks = \@riptracks;      # total_tracks is a reference to array 
    }

    # try to find discinfo from cddbserver unless -n
    if ( $nocddb == 0 ) {
        &cddb;
    } else {   
        &nocddb;
    }
    
}

#
# what to do with cddb success
#

sub cddb {
    if ( &get_cdda_infos ) {
        &rip_and_encode($total_tracks);
    } else {
        print STDERR "no cddb entry found or cddbserver unreachable\n";
        &nocddb;
    }
}

#
# what to do without cddb or failed cddb
#

sub nocddb {
    # if -q no questions will be asked
    if ($quite == 1) {
        &rip_and_encode($total_tracks,$trackname);
    } else {
        &get_cddb_input;

        if ( $answer eq "y" or $answer eq "Y" ) {
            &get_artist_name;
            &get_disc_name;
            &get_track_name($total_tracks);
            &rip_and_encode($total_tracks);
        } else {
            &rip_and_encode($total_tracks,$trackname);
        }
    }
}

#
# rip and encode the stuff!
#

sub rip_and_encode {
    
    my $trackname_prefix="";
    my $total;  # total sum of tracks to rip
    my @tracks; # array of integers, tracks to rip
    my $command; # execute this
   
    # how the function is called 
    if (@_) {
        unless (ref($total_tracks)) {
            $total=$_[0];
            my $i=1;
            while($i<=$total) { push(@tracks, $i); $i++; };
        } else {
            $total=scalar(@{$_[0]});
            @tracks=@{$_[0]};
            # check if the values are correct
            my $i=0;
            while($i<=$#tracks) { 
                if ($tracks[$i] > &get_disc_tracks) { 
                    print STDOUT "$tracks[$i] is greater than tracks on your disc\n";
                    exit 1;
                }
                $i++; 
            }
        }    
        $trackname_prefix=$_[1];
    }
    
    # print artist and disc title, but only when known
    if ($artist && $album) {       
        &print_disc_title;  
    }
   
    # be verbose or quit
    if ( $verbose != 0 ) {
        $null = "";
    } else {
        print "\n Ripping and encoding...\n";
    }
    
    # set ripper & encoderflags
    $cdpflags= $cdpflags . " " . $cdpflags_default . " " . "-d $cdrom";
    $oggencflags= $oggencflags . " " . $oggencflags_default . " " . "-b $bitrate";

    # some debugging info
    if ($debug == 1 ) {
        print STDERR "Ripperflags: $cdpflags\n";
        print STDERR "Encoderflags: $oggencflags\n";
        print STDERR "Following tracks: @tracks \n"; 
        print STDERR "Total Tracks to rip: $total \n";
    }
    
    # make directories and change currentdir
    if ($output) {
        my $path;
        $output =~ s/(.*)\///;
        $path = $1;
        if ($path) {
            $path =~ s/%a/$artist/;
            $path =~ s/%d/$album/;
            # some debug info
            if ( $debug == 1) { 
                print STDERR "path to put in files: $path\n";
            }    
            unless ( -d $path ) {
                mkdir("$path") || die "can't make directory $path: $!\n";
            }
            if ( -W $path ) {
                chdir("$path");
            } else {
                print "directory is not writable for you\n";
                exit 1;
            }    
        } 
    }    

    for (my $i=1; $i <= $total; $i++) {
        
        if ( !$output ) {
        
            if (! $trackname_prefix ) {
                $trackname="$artist - $titles[$tracks[$i-1]]";
            } else {
                $trackname="$trackname_prefix$tracks[$i-1]";
            }
            
        } else {  
            
            my $j;
            $j =$output;
            $output =~ s/%a/$artist/;
            $output =~ s/%n/$i/;
            $output =~ s/%d/$album/;
            $output =~ s/%t/$titles[$tracks[$i-1]]/;
            $trackname="$output";
            if ($debug == 1) {
                print STDERR "trackname: $trackname\n";
            }
            $output=$j;
        
        }

        # if file exists ask if it should be overwritten
        my $exists;

        if ( -e "$trackname.wav" && $quite == 0) {
            print "File $trackname.wav exists, should I override? (y/N)\n";
            chomp($exists = <STDIN>);
            if ( $exists ne "y" ) {
                $exists ="";
                next;
            }
        } 
        
        # rip tracks
        $command = "$cdparanoia $cdpflags $tracks[$i-1] \"$trackname.wav\" $null ";
        if ($debug == 1) {
            print STDERR "execute commandline: $command\n";
        }
        `$command`;

        # if file exists ask if it should be overwritten
        if ( -e "$trackname.ogg" && $quite == 0) {
            print "File $trackname.ogg exists, should I override? (y/N)\n";
            chomp($exists = <STDIN>);
            if ( $exists ne "y" ) {
                $exists ="";
                next;
            }
        } 

        # encode tracks
        $command = "$oggenc $oggencflags \"$trackname.wav\" $null ";
        if ($debug == 1) {
            print STDERR "execute commandline: $command\n";
        }
        `$command`;
        
        # remove tracks
        if ( $holdwav != 1) {
            $command = "rm -f -- \"$trackname.wav\" $null ";
            if ($debug == 1) {
                print STDERR "execute commandline: $command\n";
            }
            `$command`;
        }
    }
    
    #change back to last directory
    chdir("-");
    print "\n successfully ripped $total tracks!\n"     
    
}

#
# fetch the CD info from CDDB server
#

sub get_cdda_infos { 
    my $total; 
    my $cddb = new FreeDB;

    $cddb->cddb_server($cddbserver);
    $cddb->cdrom_device($cdrom);
    $success = $cddb->fetch;

    if (!$success) {
        print "\n\nCouldn't fetch or find CD info from $cddbserver\n\n";
        return 1;
    }

    $artist = $cddb->artist;    # fetch name of artist(s)
    $artist =~ s/"/\\"/ ;       # remove line feed at end of string

    $album = $cddb->title; # fetch the CD title
    $album =~ s/"/\\"/ ;   # remove line feed at end of string

    $total = $cddb->total_tracks;
    @titles = $cddb->track_info;

    for (my $i=0; $i < $total; $i++) {
       $titles[$i] =~ s/"/\\"/ ;
    }
    return 0;
}

#
# can cdparanoia and oggenc be found?
#

sub check_apps {
    unless( -x $oggenc ) {
        print STDERR "couldn't find or execute oggenc in $oggenc\n";
        exit 1;
    }
    unless( -x $cdparanoia ) {
        print STDERR "couldn't find or execute cdparanoia in $cdparanoia\n";
        exit 1;
    }

}

#
# subroutine for configfile parsing
#

sub parse_config_file {
    my $CDDA2OGGRC = $_[0];
    my $i=0;
    open RC, $CDDA2OGGRC or return;
    while (<RC>) {
        $i++;
        if (! (/^[\#;]/ || /^\s*$/)) {    # ignore comments and blank lines
            /^\s*(\S*)\s*=?\s*(.*)\s*$/;  # grab useful bits from line
            my @line = ("--$1", $2);
            parse_command_line (@line);
        }
    }
}

#
# subroutine for commandline parsing
#

sub parse_command_line {
    while (@_) {
        # options parser
        if ($_[0] =~ m/-/) {
            if ($_[0] eq "-r" or $_[0] eq "--ripper") {
                shift; $cdparanoia = shift; # specify path to cdparanoia
            }
            elsif ($_[0] eq "--ripperflags") {
                shift; $cdpflags = shift;   # flags to be passed to cdparanoia
            }
            elsif ($_[0] eq "-e"  or $_[0] eq "--encoder") {
                shift; $oggenc = shift;     # path to oggenc
            }
            elsif ($_[0] eq "--encoderflags") {
                shift; $oggencflags = shift;# flags to be passed to oggenc
            }
            elsif ($_[0] eq "-b" or $_[0] eq "--bitrate") {
                shift; $bitrate = shift;    # bitrate to encode files with
                unless ($bitrate =~ m/(112|128|160|192|256|350)/) {
                    print STDERR "Unrecognized bitrate: $bitrate\n\n";
                    print STDERR usage(); exit 1; 
                }   
            }
            elsif ($_[0] eq "-c" or $_[0] eq "--config") {
                shift; parse_config_file (shift);# use config file <filename>
            }
            elsif ($_[0] eq "-s" or $_[0] eq "--server") {
                shift; $cddbserver = shift; # defines CDDB server
            }
            elsif ($_[0] eq "-d" or $_[0] eq "--device") {
                shift; $cdrom = shift;      # ripping device
            }
            elsif ($_[0] eq "-o" or $_[0] eq "--output") {
                shift; $output = shift;      # output format
            }
            elsif ($_[0] eq "-n" or $_[0] eq "--nocddb") {
                shift; $nocddb = 1;         # do not use CDDB if 1
            }
            elsif ($_[0] eq "-q" or $_[0] eq "--quite") {
                shift; $quite = 1;        # be verbose if 1
            }
            elsif ($_[0] eq "-D" or $_[0] eq "--debug") {
                shift; $debug = 1;        #  turn on debugging
            }
            elsif ($_[0] eq "-v" or $_[0] eq "--verbose") {
                shift; $verbose = 1;        # be verbose if 1
            }
            elsif ($_[0] eq "-w" or $_[0] eq "--holdwav") {
                shift; $holdwav = 1;        # do not delete .wav files if 1
            }
            elsif ($_[0] eq "-t" or $_[0] eq "--trackname") {
                shift; $trackname = shift;  # define track name format?
            }
            elsif ($_[0] eq "-h" or $_[0] eq "--help") {
                print usage(); exit 0;      # print out usage on screen
            }
            elsif ($_[0] eq "-V" or $_[0] eq "--version") {
                print "$version\n"; exit 0;    # print out version number on screen
            } else {
                print STDERR "Unrecognized option: $_[0]\n\n";
                print STDERR usage(); exit 1;
            }    
        # commandline parser    
        } elsif ($_[0]) {
            if ($_[0] eq "all") {
                shift; $all=1;              # whole disc?
            }
            elsif ($_[0] =~ m/[^0-9,]/) {
                print STDERR "Unrecognized option: $_[0]\n\n";
                print STDERR usage(); exit 1;
            }
            elsif ($_[0] =~ m/(([0-9],?)+)/) {
                shift; @riptracks=split /,/, $1;       # some tracks
            } else {
                print STDERR "Unrecognized option: $_[0]\n\n";
                print STDERR usage(); exit 1;
            }    
        }    
	
	# if nothing of the above matches...

        else {
            print STDERR "Unrecognized option: $_[0]\n\n";
            print STDERR usage(); exit 1;
        }
    }
}


#
# print out version number, authors and license
#

sub version {

    print "\n$cdda2ogg version $version\n\nCopyright 2001\n$author\nhttp://cdda2ogg.berlios.de\n";
    print "- Released without any warranty under the terms of the GNU Public Licence -\n\n";

}

#
# print out usage
#

sub usage {
    return sprintf  <<END
usage: $cdda2ogg [options] tracks 
        
options:
  -o or --output "string"    read "string" as cdda2ogg output format, please do not forget ""
                             %%a => Artist , %%l => Album
                             %%t => Title  , %%n => Tracknumber
                             [default="%%a - %%t"]
  -c or --config <filename>  read <filename> as cdda2ogg configuration file
  -r or --ripper <path>      specify <path> to ripper
                             [default=/usr/bin/cdparanoia]
  --ripperflags "flags"      pass "flags" to ripper
                             [default=""]
  -e or --encoder <path>     specify <path> to encoder
                             [default=/usr/bin/oggenc]
  --encoderflags "flags"     pass "flags" to encoder
                             [default=""]
  -b or --bitrate <bitrate>  use <bitrate> for encoding [112,128,160,192,256,350]
                             [default=160]
  -s or --server <url>       use <url> to ask CDDB server
                             [default="http://freedb.freedb.org/cgi-bin/cddb.cgi"]
  -n or --nocddb             do not attempt CDDB lookup
  -w or --holdwav            do not delete wav files after encoding
  -d or --device <device>    cdrom\/dvd device 
                             [default=/dev/cdrom]
  -t or --trackname "string" use "string" as trackprefix name for tracktitles
                             [default="track"] 
  -v or --verbose            verbose output, print original output to STDOUT
  -D or --debug              debug output, print many messages to STDERR
  -q or --quite              do not ask any fu.. questions, 
                             be warned, files with eqal names get lost
  -V or --version            print version
  -h or --help               print help message

tracks: 
    could be "all" or comma separated list of tracknumbers
    example: 2,3,15  
    
END
      ;
}

#
# this procedure prints out the disc title and artist on startup
#

sub print_disc_title {

print "\n\n\n";
print "The inserted disc is '$album' from '$artist'.\n";
print "--------------------------------------------------------------------------------\n\n";
print "Hope you\'ll enjoy their music as Ogg Vorbis file with $bitrate bits/sec.!\n\n";

}

#
# get the number of audiotracks, returns an integer
# 

sub get_disc_tracks {
    my $cdromheader = ""; 
    my $result;
    my $start;
    open(CDROM, $cdrom) || die "$! - Please insert a audio disc in your drive or check permissions
    on $cdrom\n";
    $result = ioctl(CDROM, 0x5305, $cdromheader);    
    ($start, $total_tracks) = unpack('CC', $cdromheader);
    close(CDROM);
    return $total_tracks;
}

#
# ask if the user want to give needed information, returns a string
#

sub get_cddb_input {
    print "Do you want to tell me artist, album and tracktitle's? (y/n)\n";
    chomp($answer = <STDIN>);
    return $answer;
}

#
# ask for artistname, returns a string
#

sub get_artist_name {
    print "Please tell me the name of the artist:\n";
    chomp($artist = <STDIN>);  
    return $artist;
}   

#
# ask for disc title
#

sub get_disc_name {
    print "Please tell me the name of the album:\n";
    chomp($album = <STDIN>);  
    return $album;
}   

#
# ask for tracktitles, returns an array 
#

# FIXME
sub get_track_name {
    if ( ! @riptracks ) {
        for (my $i=1; $i <= $total_tracks; $i++) {
            print "Please tell me the title of track $i:\n";
            chomp($titles[$i-1] = <STDIN>);
        }
    } else {
        for (my $i=0; $i <= $#riptracks; $i++) {
            print "Please tell me the title of track $riptracks[$i]:\n";
            chomp($titles[$i] = <STDIN>);
        } 
    }
    return @titles;
}        


=head1 NAME

CDDA2OGG - Compact Disc Digital Audio to Ogg/Vorbis

=head1 SYNOPSIS

cdda2ogg       B<--help> B<--verbose> B<--device> I<devicename> B<--quiet>

=head1 DESCRIPTION

CDDA2OGG is a small script written in Perl. It can be used 
to rip Audio CDs and encode the songs as Ogg/Vorbis file.

=head1 OPTIONS

=over

=item B<--help>

Prints a short summary of options.

=back

=head1 EXAMPLES

For ripping the whole audio disc in your cdrom drive:
        
C<cdda2ogg -d /dev/cdrom all>

=head1 RESTRICTIONS

Up to now CDDA2OGG does not support uploading of CDDB data. 
You can't configure the output naming at the moment. 
Error handling is not very well.

=head1 AUTHOR

The CDDA2OGG project was created by Waldemar Brodkorb <waldemar@thinknow.de>
My good friend Richard Höchenberger is helping me, and working on a
GUI for CDDA2OGG.

For more information on CDDA2OGG, see: http://cdda2ogg.berlios.de/

=head1 LICENSE

Copyright (C) 1999-2001 Waldemar Brodkorb <waldemar@thinknow.de>

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 675 Mass
Ave, Cambridge, MA 02139, USA. 


